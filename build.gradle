buildscript {
    repositories {
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.+'
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

allprojects {
    repositories {
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 21
    buildToolsVersion '21.1.1'

    defaultConfig {
        /*
         * Official package name used by the Google Play store.
         * WARNING: Changing this will create a new app with no history.
         */
        applicationId 'com.aircandi.catalina'
        minSdkVersion 14
        targetSdkVersion 21
        versionCode 188
        versionName '1.5.6'
        multiDexEnabled true
    }

    signingConfigs {
        release {
            storeFile file('aircandi.keystore')
            keyAlias 'aircandi'
            storePassword 'Doo.dah.1'
            keyPassword 'Doo.dah.1'
        }
    }

    buildTypes {
        /*
         * shrinkResources will remove unused resources from build. A resource will
         * be considered used if we have it associated with a theme attr even if that
         * attr is not being used.
         */
        debug {
            minifyEnabled false  // TODO: Remove once we shift to granular dependencies for play services.
            debuggable true
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
            zipAlignEnabled true
            debuggable false
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def file = output.outputFile
                    output.outputFile = new File(file.parent, file.name.replace(".apk", "-" + defaultConfig.versionName + ".apk"))
                }
            }
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
    }

    dexOptions {
        jumboMode true
        incremental true
        preDexLibraries false
        javaMaxHeapSize "4g"
    }
}

dependencies {

    compile 'com.android.support:support-v4:21.+'
    compile 'com.android.support:appcompat-v7:21.+'
    compile 'com.android.support:multidex:1.0.0'
    compile 'com.google.android.gms:play-services:6.5.+'
    compile 'com.google.maps.android:android-maps-utils:0.3+'
    compile 'com.squareup.okhttp:okhttp:2.0.0'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.0.0'
    compile 'com.squareup.okio:okio:1.0.0'
    compile 'com.squareup:otto:1.3.5'
    compile 'com.squareup.picasso:picasso:2.4.+'
    compile 'net.minidev:json-smart:1.2'
    compile 'it.sephiroth.android.library.imagezoom:imagezoom:+'
    compile 'com.amazonaws:aws-android-sdk-core:2.1.+'
    compile 'com.amazonaws:aws-android-sdk-s3:2.1.+'
    /*
     * File libs are done individually so we know exactly what is being used.
     */
    compile files('libs/image-chooser-library-1.3.1.jar')
    compile files('libs/cwac-adapter-wrapper.jar')
    compile files('libs/endless-1.2.3.jar')

    compile('com.crashlytics.sdk.android:crashlytics:2.1.0@aar') {
        transitive = true;
    }
}