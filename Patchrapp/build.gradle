apply plugin: 'com.android.application'
apply plugin: 'android-apt'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'realm-android'

android {

	compileSdkVersion 24
	buildToolsVersion '24.0.0'

	defaultConfig {
		/*
         * Official package name used by the Google Play store.
         * WARNING: Changing this will create a new app with no history.
         */
		applicationId 'com.patchr.android'

		minSdkVersion 16        // ICS 4.1 and up: Jelly Bean
		targetSdkVersion 24     // Marshmallow
		versionCode 131
		versionName '1.3.7'

		resConfigs "en"
		multiDexEnabled true
		manifestPlaceholders = [
				appPackageName: "${applicationId}",
				manifestApplicationId: "${applicationId}",
				onesignal_app_id: "f43fe789-3392-4f2b-bf52-950d0c78fffe",
				onesignal_google_project_number: "657673071389"
		]
		buildConfigField 'String', 'PROXI_SERVICE_URI', '"https://api.aircandi.com/v1/"'
		buildConfigField 'String', 'SEARCH_SERVICE_URI', '"https://api.cognitive.microsoft.com/bing/v5.0/images/"'
	}

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}

	sourceSets {
		main {
			res.srcDirs = ['src/main/res']
			java {
				exclude '**/SegmentProvider.java'
			}
		}
	}

	lintOptions {
		checkReleaseBuilds true
	}

	signingConfigs {
		release {
			try {
				storeFile file('app.keystore')
				keyAlias 'aircandi'
				storePassword KEYSTORE_PASSWORD
				keyPassword KEY_PASSWORD
			}
			catch (ex) {
				throw new InvalidUserDataException("You should define KEYSTORE_PASSWORD and KEY_PASSWORD in gradle.properties.")
			}
		}
	}

	productFlavors {    /* Stacks on top of defaultConfig */
		dev {
			minSdkVersion 16
		}
		prod {
			/* The actual minSdkVersion for the released application. */
			minSdkVersion 16
		}
	}

	buildTypes {

		debug {
			debuggable true
			minifyEnabled true
			shrinkResources true
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
			buildConfigField 'String', 'PROXI_SERVICE_URI', '"http://api.ariseditions.com:8080/v1/"'
		}

		/*
         * shrinkResources will remove unused resources from build. A resource will
         * be considered used if we have it associated with a theme attr even if that
         * attr is not being used.
         */
		release {
			minifyEnabled true
			shrinkResources true
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
			signingConfig signingConfigs.release
			zipAlignEnabled true
			debuggable false
			applicationVariants.all { variant ->
				variant.outputs.each { output ->
					def file = output.outputFile
					def fileName = file.name.replace(".apk", "-" + defaultConfig.versionName + ".apk")
					output.outputFile = new File(file.parent, fileName)
				}
			}
		}
	}

	packagingOptions {
		pickFirst 'AndroidManifest.xml'
		exclude 'META-INF/DEPENDENCIES'
		exclude 'META-INF/notice.txt'
		exclude 'META-INF/license.txt'
		exclude 'META-INF/LICENSE.txt'
		exclude 'META-INF/LICENSE'
		exclude 'META-INF/NOTICE.txt'
		exclude 'META-INF/NOTICE'
		exclude 'META-INF/MANIFEST.MF'
		exclude '!META-INF/MANIFEST.MF'
		exclude 'META-INF/services/javax.annotation.processing.Processor'
	}

	dexOptions {
		jumboMode true
		dexInProcess true
		preDexLibraries false
		javaMaxHeapSize "2g"
	}
}

dependencies {
	/* External services */
	/* Includes tag manager */
	/* Components */
	/* Switch to release 2.6.0 when available */
	/* UI */
	/* Address tokens */
	/* Map clustering */
	/* Used for guest guard dialog */
	retrolambdaConfig 'net.orfjackal.retrolambda:retrolambda:2.3.0'
	//noinspection NewerVersionAvailable
	compile 'com.amazonaws:aws-android-sdk-s3:2.3.0'
	compile 'com.facebook.android:facebook-android-sdk:4.14.1'
	compile 'com.facebook.android:account-kit-sdk:4.14.0'
	compile 'com.parse:parse-android:1.13.1'
	compile 'com.parse.bolts:bolts-applinks:1.4.0'
	compile 'io.branch.sdk.android:library:2.0.2'
	compile 'com.bugsnag:bugsnag-android:3.5.0'
	compile 'com.google.android.gms:play-services-base:9.4.0'
	compile 'com.google.android.gms:play-services-location:9.4.0'
	compile 'com.google.android.gms:play-services-maps:9.4.0'
	compile 'com.google.android.gms:play-services-analytics:9.4.0'
	compile 'com.google.android.gms:play-services-gcm:9.4.0'
	compile 'com.squareup.okhttp3:okhttp:3.4.1'
	compile 'com.squareup.okhttp3:logging-interceptor:3.4.1'
	compile 'com.squareup.okio:okio:1.9.0'
	compile 'com.squareup.picasso:picasso:2.6.0-SNAPSHOT'
	compile 'com.squareup.retrofit2:retrofit:2.1.0'
	compile 'com.squareup.retrofit2:converter-gson:2.1.0'
	compile 'com.squareup.retrofit2:adapter-rxjava:2.1.0'
	compile 'io.reactivex:rxandroid:1.2.1'
	compile 'io.reactivex:rxjava:1.1.8'
	compile 'com.facebook.stetho:stetho:1.3.1'
	compile 'com.facebook.stetho:stetho-okhttp3:1.3.1'
	compile 'com.uphyca:stetho_realm:0.9.0'
	compile 'org.greenrobot:eventbus:3.0.0'
	compile 'com.android.support:multidex:1.0.1'
	compile 'com.birbit:android-priority-jobqueue:2.0.0-beta3'
	compile 'com.android.support:support-v4:24.1.1'
	compile 'com.android.support:appcompat-v7:24.1.1'
	compile 'com.android.support:cardview-v7:24.1.1'
	compile 'com.android.support:design:24.1.1'
	compile 'com.android.support:recyclerview-v7:24.1.1'
	compile 'com.splitwise:tokenautocomplete:2.0.7@aar'
	compile 'com.google.maps.android:android-maps-utils:0.4.3'
	compile 'com.commit451:PhotoView:1.2.4'
	compile 'com.kbeanie:image-chooser-library:1.6.0@aar'
	compile 'com.github.machinarius:preferencefragment:0.1.2'
	compile 'com.flipboard:bottomsheet-core:1.5.0'
	compile 'com.flipboard:bottomsheet-commons:1.5.0'
	compile 'com.adobe.creativesdk.foundation:auth:0.9.1062'
	compile 'com.adobe.creativesdk:image:4.6.3'
	compile 'com.github.arimorty:floatingsearchview:2.0.2'
	compile 'com.makeramen:roundedimageview:2.2.1'
	compile 'com.localytics.android:library:3.8.0'
	compile 'com.onesignal:OneSignal:3.+@aar'
}

apply plugin: 'com.google.gms.google-services'